#!/bin/env perl
BEGIN {
  my $mydir = $0;
  $mydir =~ s|[^/]+$||;
  my $pugsroot = $ENV{PUGS_ROOT} || $mydir."../../..";
  my $p6dir = $pugsroot."/src/perl6";
  unshift(@INC,$p6dir);
}
use strict;
use warnings;
use STD;
use utf8;
use Encode;
use File::Temp qw(tempfile);
use Digest::MD5 qw(md5_hex);
use Data::Dumper;

binmode(STDOUT, ":utf8");
binmode(STDERR, ":utf8");

sub print_usage_and_exit {
  my($msg,$code)=@_;
  $code = 2 if !defined $code;
  print $msg,"\n\n" if $msg;
  print STDERR <<END;
$0 [-q] [--format=FORMAT] [ --start=RULE ] [ FILENAME | -e CODE ]

--start is ignored.  See src/perl6/try5 for how to do it.

Set STD_RED_CACHEDIR as with STD_red_run.

FORMATs
 p5a

END
  exit($code);
}

sub slurp {
  my($filename)=@_;
  open(F,'<:utf8',$filename) or die "Can't open $filename: $!\n";
  my $text; local $/; $text = <F>; close F;
  $text;
}
sub unslurp {
  my($filename,$text)=@_;
  open(F,'>:utf8',$filename) or die "Can't open $filename: $!\n";
  print F $text; close F;
  undef;
}

# for command line, see ../../STD_red/STD_red_run
my($error_message,$quiet,$format,$start,$filename,$code,$dash_e)=@_;

my $file;
sub main {
  print_usage_and_exit if !@_;
  print_usage_and_exit(undef,0) if @_ == 1 && $_[0] eq '--help';
  if($_[0] =~ /\A--error-message=(.+)/) {
    $error_message = $1;
    shift;
  }
  if($_[0] eq '-q') {
    $quiet = 1;
    shift; 
  }
  if($_[0] =~ /\A--format=(\w+)/) {
    $format = $1;
    shift;
  }
  if($_[0] =~ /\A--start=(\w+)/) {
    $start = $1;
    shift;
  }
  if($_[0] eq '-e') {
    shift;
    $code = shift;
    utf8::is_utf8($code) && die;
    $code = Encode::decode_utf8($code);
    utf8::is_utf8($code) || die;
    $dash_e = 1;
  }
  elsif(@_) {
    $filename = shift;
    print_usage_and_exit("File $filename doesn't exist.") if !-e $filename;
    $code = slurp($filename);
  }
  else {
    print_usage_and_exit
  }

  my($cache_file,$output) = cached_output_for($code, $format);
  $output = undef; #XXX disable cache
  if($output && !$dash_e){print $output; exit;}

  if(!$filename) {
    my $fn;
    ($fn,$filename) = tempfile(undef, UNLINK => 1);
    binmode($fn,":utf8");
    print $fn $code;
    close($fn);
  }

  my $r = STD->parsefile($filename);

  if(!$r) {
    print STDERR "Parse failed.\n";
    exit(1);
  }

  if(!$format) {
    #print STD::Dump($r);
    #use YAML::XS;
    #print Dump($r);
    print Data::Dumper::Dumper($r);
#    use DumpMatch; #src/perl6/STD5_dump_match
#    print dump_match("comp_unit",$r,{vertical=>1});
  } 
  elsif($format eq 'p5a') {
    $main::whole_file = $code;
    $main::whole_file .= " "; # -e '3' dump includes _pos's of 2
    print out($cache_file,$r->to_dump0);
  }
  else {
    print_usage_and_exit
  }
}
sub cached_output_for {
  my($code,$format)=@_;
  my $cachedir = $ENV{STD_RED_CACHEDIR};
  return undef if !$cachedir || !$format;
  my $input = $code.$format;
  my $input_sig = md5_hex($input);
  my $cache_file = $cachedir.'/parse_blue_'.substr($input_sig,0,32);
  my $output;
  if(-e $cache_file) {
    $output = slurp($cache_file);
  }
  return($cache_file,$output);
}
sub out {
  my($cache_file,$output)=@_;
  if($cache_file) {
    unslurp($cache_file,$output);
  }
  $output;
}

# Notes
# For to_dump0, see ../../STD_red/match.rb, bottom.
# The STD graph seems generally cyclic. :/
use autobox;
{ package STD;
  sub condition_sym {
    my($o,$sym)=@_;
    if (ref $sym) { join(" ",@$sym) }
    elsif ($sym !~ /^sym/) { $sym }
    elsif ($sym =~ /\Asym<\s*(.+?)\s*>\z/) { $1 }
    elsif ($sym =~ /\Asym«\s*(.+?)\s*»\z/) { $1 }
    elsif ($sym =~ /\Asym\['(.+?)']\z/) { $1 }
    else { die "bogus sym: $sym\n" }
  }
  our %seen;
  our $max_repetition = 3;
  sub to_dump0 {
    my($o)=@_;
    local $seen{$o} = $seen{$o};
    if($seen{$o}++ > $max_repetition) { return "LOOP:$o"->to_dump0 }

    my $rule = $o->{_reduced} || do{
      warn "Untagged Match tree node:\n".Data::Dumper::Dumper($o)."\n";
      "unknown"
    };

    my $f = $o->{_from};
    my $t = $o->{_pos};
    my $str = substr($main::whole_file,$f,$t-$f);

    my $sym = $o->{sym};
    if($sym && $sym eq ';') {
      # Workaround bug in STD/gimme5 r22962.
      # -e 'last;' -> term:sym<;> instead of term:sym<last>.
      $sym = $str;
    }
    if($sym) {
      my $normalized = $o->condition_sym($sym);
      $o->{sym_name} = $normalized;
      $rule .= ":".$normalized if ($sym ne $rule &&
                                   $rule ne 'EXPR' &&
                                   $rule ne 'infixish');
    }
    my $rule_str = $rule->to_dump0;
    my $s = $str->to_dump0;
    my $h = join("",map{
      if(!/^[a-zA-Z]/) { "" }
      elsif(/\A(O)\z/) { "" }
      else {
        my $v = $o->{$_};
        my $vs = defined($v) ? $v->to_dump0 : 'undef';
        "\n $_ => $vs,"
      }
    }keys(%$o));
    "match($rule_str,$s,$f,$t,{$h})"
  }
}
{ package ARRAY;
  sub to_dump0 {
    my($o)=@_;
    '['.join(",",map{$_->to_dump0}@$o).']'
  }
}
{ package HASH;
  sub to_dump0 {
    my($o)=@_;
    '{'.join(",",map{$_.' => '.$o->{$_}->to_dump0}keys(%$o)).'}'
  }
}
{ package SCALAR;
  sub to_dump0 {
    my($o)=@_;
    $o =~ s/([\\'])/\\$1/g;
    "'".$o."'";
  }
}


main(@ARGV);


